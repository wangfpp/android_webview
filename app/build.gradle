import groovy.json.JsonSlurper

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("GMT+08:00"));
}

def getVersion() {
    File versionFile = new File('version.json');
    def jsonLoad = new JsonSlurper();
    def versionJson = jsonLoad.parseText(versionFile.text);
    return  versionJson["version"];
}

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.study.android_wv"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName getVersion();

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        def buildName = variant.buildType.name
        def createTime = releaseTime()
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // https://developer.android.com/studio/build/configure-apk-splits.html
            if (buildName == 'release') {
                def outputFileName = "webview_study-${variant.versionName}_${createTime}.apk"
                // println output.getPackageApplicationProvider()
                output.outputFileName = outputFileName
            }

        }
    }
}

dependencies {
    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}